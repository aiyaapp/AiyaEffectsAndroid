apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 配置 jCenter 路径
group = "com.aiyaapp.aiya"
def libraryName = project.name
version = android.defaultConfig.versionName

// 定义相关网站
def siteUrl = 'https://github.com/aiyaapp/AiyaEffectsAndroid'  // 项目的主页
def gitUrl = 'https://github.com/aiyaapp/AiyaEffectsAndroid.git'

install {
  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      project {
        packaging 'aar'
        // Add your description here
        name libraryName
        url siteUrl
        // Set your license
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id 'aiya'        //填写的一些基本信息
            name 'aiya'
            email 'liudawei@aiyaapp.com' // 填写邮箱
          }
        }
        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl
        }
      }
    }
  }
}

// 打包 javadocjar 和 sourcejar
task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}
task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  options.encoding "UTF-8"
  options.charSet 'UTF-8'
  options.author true
  options.version true
  failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
artifacts {
  archives javadocJar
  archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")
  configurations = ['archives']
  pkg {
    repo = "sdk"
    name = libraryName    //发布到JCenter上的项目名字
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = ["Apache-2.0"]
    publish = true
  }
}
